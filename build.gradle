buildscript {
    repositories {
    	maven { url  = 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
	id 'fabric-loom' version '0.8-SNAPSHOT'
	id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {	
		url = 'https://maven.fabricmc.net/io/github/prospector/modmenu/'
	}
	maven {
		name = "Ladysnake Libs"
		url = "https://ladysnake.jfrog.io/artifactory/mods"
	}
	maven {
        url = "https://www.cursemaven.com"
    }
     maven {
        name = "OnyxStudios"
        url = "https://maven.abusedmaster.xyz"
    }
    maven {
        name "Siphalor's Maven"
        url "https://maven.siphalor.de"
    }
    maven { url "https://maven.shedaniel.me/" }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation("software.bernie.geckolib:geckolib-fabric-1.17:3.0.11:dev") {
    	exclude(group: "com.terraformersmc.modmenu")
        exclude(group: "io.github.prospector")
        exclude(group: "me.shedaniel")
        exclude(group: "net.fabricmc")
    }
    modCompileOnly("vazkii.patchouli:Patchouli:1.17-55-FABRIC-SNAPSHOT") {
    	exclude(group: "com.terraformersmc.modmenu")
        exclude(group: "io.github.prospector")
        exclude(group: "me.shedaniel")
        exclude(group: "net.fabricmc")
    }
	modImplementation 'curse.maven:trinkets-341284:3387025'
    modApi("me.shedaniel.cloth:cloth-config-fabric:5.0.34") {
    	exclude(group: "com.terraformersmc.modmenu")
    }
	modCompileOnly("de.siphalor:nbtcrafting-1.17:2.0.20+mc1.17.1") {
    	exclude(group: "com.terraformersmc.modmenu")
        exclude(group: "io.github.prospector")
        exclude(group: "me.shedaniel")
        exclude(group: "net.fabricmc")
    }
	include("de.siphalor:nbtcrafting-1.17:2.0.20+mc1.17.1") {
    	exclude(group: "com.terraformersmc.modmenu")
        exclude(group: "io.github.prospector")
        exclude(group: "me.shedaniel")
        exclude(group: "net.fabricmc")
    }	
	modImplementation 'curse.maven:REI-310111:3424130'
	modImplementation 'curse.maven:architectury-fabric-419697:3427341'
	modImplementation 'curse.maven:modmenu-308702:3348711'
	modImplementation 'curse.maven:TDI-245733:3358651'
	modImplementation 'curse.maven:cca-318449:3380730'
	modImplementation 'curse.maven:ccaentity-318449:3380734'
	modImplementation 'curse.maven:ccaworld-318449:3380739'
	modImplementation 'curse.maven:ccabase-318449:3380731'
}

processResources {
    inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each { 
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

minecraft {
	accessWidener "src/main/resources/doom.accesswidener"
}

if (file('key.properties').exists()) {
    curseforge {

    	def curseProp = new Properties()
    	File secretPropsFile = file("key.properties")
    	curseProp.load(secretPropsFile.newInputStream())

    	project {
        	apiKey = curseProp.getProperty('curseKey')
        	id = "${curse_id}"
        	releaseType = "${curse_release}"
        	changelogType = 'text'
        	changelog = file('CHANGELOG.txt')
			addGameVersion("1.17.1")
			addGameVersion("Fabric")
			addGameVersion("Java 16")

        	relations {
            	requiredDependency 'fabric-api'  
            	requiredDependency 'geckolib' 
            	optionalDependency 'patchouli-fabric'   
            	requiredDependency 'trinkets-fabric'   
            	requiredDependency 'cloth-config'  
        	}
        	mainArtifact(remapJar)

        	afterEvaluate {
            	uploadTask.dependsOn("remapJar")
        	}
    	}
    	options {
    		//debug = true
        	forgeGradleIntegration = false
    	}
    }
}