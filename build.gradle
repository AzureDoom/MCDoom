buildscript {
    repositories {
    	maven { url  = 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.0"
    }
}

plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput
import com.modrinth.minotaur.dependencies.ModDependency

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven { url "https://cfa2.cursemaven.com" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url 'https://dl.cloudsmith.io/public/azuredoom-mods/azurelib/maven/' }
    maven { url 'https://dl.cloudsmith.io/public/tslat/sbl/maven/' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url 'https://maven.terraformersmc.com/' }
    maven { url 'https://maven.kosmx.dev/' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://api.modrinth.com/maven' }
}

configurations {
    shade
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation 'mod.azure.azurelib:azurelib-fabric-1.19.2:1.0.27'
    modImplementation "curse.maven:SBL-661293:4312490-sources-4312491"
    modImplementation "maven.modrinth:configuration:nGocYE6n"
    shade group: 'maven.modrinth', name: 'configuration', version: 'nGocYE6n'
    modImplementation "maven.modrinth:modmenu:4.2.0-beta.2"
    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:9.1.643"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-api-fabric:9.1.643"
    modCompileOnly "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:9.1.643"
    modImplementation "curse.maven:trinkets-341284:4100104"
    modImplementation "curse.maven:cardinal-components-318449:3954770"
    modImplementation "curse.maven:cardinal-components-api-318449:3954769"
    modImplementation "curse.maven:cardinal-components-entity-318449:3954773"
}

shadowJar {
    configurations = [project.configurations.shade]
    from sourceSets.main.output
    duplicatesStrategy = 'exclude'
    relocate 'dev.toma.configuration', 'mod.azure.doom.shadowed.configuration'
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each { 
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
	withSourcesJar()
}

jar {
	from "LICENSE"
}

artifacts {
    archives shadowJar
}

remapJar {
    dependsOn(shadowJar)
    inputFile = shadowJar.archiveFile
}

loom {
	accessWidenerPath = file("src/main/resources/doom.accesswidener")
}

if (file('key.properties').exists()) {
    curseforge {

    	def curseProp = new Properties()
    	File secretPropsFile = file("key.properties")
    	curseProp.load(secretPropsFile.newInputStream())

    	project {
        	apiKey = curseProp.getProperty('curseKey')
        	id = "${curse_id}"
        	releaseType = "${curse_release}"
        	changelogType = 'text'
        	changelog = file('CHANGELOG.txt')
        	addGameVersion("1.19.2")
        	addGameVersion("Fabric")
        	addGameVersion("Quilt")
        	addGameVersion("Java 17")

        	relations {
            		requiredDependency 'fabric-api'  
            		requiredDependency 'azurelib'  
            		requiredDependency 'trinkets'  
            		requiredDependency 'smartbrainlib'  
        		optionalDependency 'better-combat-by-daedelus' 
        	}
        	mainArtifact(remapJar)

        	afterEvaluate {
            	uploadTask.dependsOn("remapJar")
        	}
    	}
    	options {
    		//debug = true
        	forgeGradleIntegration = false
    	}
    }
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())
		
		token = modrinthProp.getProperty('modrinthKey')
		projectId = 'Mfo4FiDF' 
		versionNumber = project.mod_version 
		versionName = 'Fabric/Quilt 1.19.2'
		uploadFile = remapJar 
		changelog = rootProject.file("changelog.txt").text
		gameVersions = ['1.19.2']
		loaders = ['fabric','quilt']
		dependencies { 
        		required.project "azurelib"
        		required.project "trinkets"
        		required.project "fabric-api"
        		required.project "smartbrainlib"
        		optional.project "better-combat"
        		optional.project "modmenu"
   		}
	}
}
