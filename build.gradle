buildscript {
    repositories {
    	maven { url  = 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
	id 'org.quiltmc.loom' version '0.12-SNAPSHOT'
	id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput
import com.modrinth.minotaur.dependencies.ModDependency

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://cfa2.cursemaven.com" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url "https://api.modrinth.com/maven" }
	maven { url = "https://maven.terraformersmc.com/" }
	maven {url = "https://ladysnake.jfrog.io/artifactory/mods" }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings( loom.layered {
		addLayer quiltMappings.mappings( "org.quiltmc:quilt-mappings:$minecraft_version+build.$quilt_mappings:v2" )
		//addLayer loom.officialMojangMappings()
	})
	modImplementation "org.quiltmc:quilt-loader:$loader_version"
	modImplementation "org.quiltmc:qsl:$qsl_version"
	modImplementation "org.quiltmc.quilted-fabric-api:quilted-fabric-api:$quilted_fapi_version-$minecraft_version"
    modImplementation 'software.bernie.geckolib:geckolib-quilt-1.18:3.0.0'
    modImplementation 'curse.maven:cloth-348521:3761416'
    modImplementation ("dev.emi:trinkets:3.3.0") {
  		exclude group: "net.fabricmc.fabric-api"
  		exclude group: "net.fabricmc", module: "fabric-loader"
	}
	modImplementation "curse.maven:RoughlyEnoughItems-310111:3768212"
	modImplementation ("curse.maven:arch-419699:3784130") {
  		exclude group: "net.fabricmc.fabric-api"
  		exclude group: "net.fabricmc", module: "fabric-loader"
	}
	modImplementation "curse.maven:stfu-401978:3677546"
    modImplementation ("me.shedaniel.cloth:basic-math:0.6.1")
}

processResources {
	inputs.property "version", version

	filesMatching('quilt.mod.json') {
		expand "version": version
	}
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each { 
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

tasks.withType(JavaCompile).configureEach {
	it.sourceCompatibility = 17 // for the IDE support
	it.options.encoding = 'UTF-8'
	it.options.release.set(17)
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

loom {
	accessWidenerPath = file("src/main/resources/doom.accesswidener")
}

if (file('key.properties').exists()) {
    curseforge {

    	def curseProp = new Properties()
    	File secretPropsFile = file("key.properties")
    	curseProp.load(secretPropsFile.newInputStream())

    	project {
        	apiKey = curseProp.getProperty('curseKey')
        	id = "${curse_id}"
        	releaseType = "${curse_release}"
        	changelogType = 'text'
        	changelog = file('CHANGELOG.txt')
			addGameVersion("1.18.2")
			addGameVersion("Quilt")
			addGameVersion("Java 17")

        	relations {
            	requiredDependency 'geckolib'  
            	requiredDependency 'trinkets' 
            	optionalDependency 'roughly-enough-items'  
            	optionalDependency 'better-combat-by-daedelus' 
        	}
        	mainArtifact(remapJar)

        	afterEvaluate {
            	uploadTask.dependsOn("remapJar")
        	}
    	}
    	options {
    		//debug = true
        	forgeGradleIntegration = false
    	}
    }
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())
		
		token = modrinthProp.getProperty('modrinthKey')
		projectId = 'Mfo4FiDF' 
		versionNumber = project.mod_version 
		versionName = 'Quilt 1.18.2'
		uploadFile = remapJar 
		changelog = rootProject.file("changelog.txt").text
		gameVersions = ['1.18.2']
		loaders = ['quilt']
		dependencies { 
        	required.project "geckolib"
        	required.project "trinkets"
        	required.project "qsl"
        	optional.project "better-combat"
            optional.project "modmenu"
            optional.project "roughly-enough-items"
   		}
	}
}