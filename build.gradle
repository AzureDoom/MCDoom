import groovy.json.JsonOutput
import groovy.json.JsonSlurper

plugins {
    id 'fabric-loom' version '1.2-SNAPSHOT' apply(false)
    id 'net.neoforged.gradle' version '[6.0,6.2)' apply(false)
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT' apply(false)
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT' apply(false)
    id 'com.matthewprenger.cursegradle' version '1.4.0' apply(false)
    id "com.modrinth.minotaur" version "2.+" apply(false)
    id "com.matyrobbrt.pngoptimiser" version "0.2.0"
}

subprojects {
    apply plugin: 'java'

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)
    java.withSourcesJar()
    java.withJavadocJar()

    jar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
        manifest {
            attributes([
                    "Specification-Title"     : "Doom",
                    "Specification-Vendor"    : "AzureDoom",
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.mod_version,
                    "Implementation-Vendor"   : "AzureDoom",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }

    sourcesJar {
        from(rootProject.file("LICENSE")) {
            rename { "${it}_${mod_name}" }
        }
    }

    repositories {
        mavenCentral()
        maven {
            name = 'Sponge / Mixin'
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        maven {
            name = 'BlameJared Maven (JEI / CraftTweaker / Bookshelf)'
            url = 'https://maven.blamejared.com'
        }
        maven {
            url = 'https://dl.cloudsmith.io/public/tslat/sbl/maven/'
        }
    }

    tasks.withType(JavaCompile).configureEach {

        it.options.encoding = 'UTF-8'
        it.options.getRelease().set(17)
    }

    processResources {
        def expandProps = [
                "mod_version"            : project.mod_version,
                "group"                  : project.group, //Else we target the task's group.
                "minecraft_version"      : project.minecraft_version,
                "neo_version"            : project.neo_version,
                "loader_version_range"   : project.loader_version_range,
                "neo_version_range"      : project.neo_version_range,
                "minecraft_version_range": project.minecraft_version_range,
                "fabric_version"         : project.fabric_version,
                "fabric_loader_version"  : project.fabric_loader_version,
                "mod_name"               : project.mod_name,
                "mod_author"             : project.mod_author,
                "mod_id"                 : project.mod_id,
                "mod_license"            : project.mod_license,
                "mod_description"        : project.mod_description,
                "mod_credits"            : project.mod_credits,
                "mod_logo"               : project.mod_logo,
                "mod_url"                : project.mod_url,
                "mod_issues"             : project.mod_issues,
                "mod_sources"            : project.mod_sources,
        ]

        filesMatching(['pack.mcmeta', 'fabric.mod.json', '*.mixins.json', 'META-INF/mods.toml']) {
            expand expandProps
        }
        inputs.properties(expandProps)
        doLast {
            def jsonMinifyStart = System.currentTimeMillis()
            def jsonMinified = 0
            def jsonBytesSaved = 0
            fileTree(dir: outputs.files.asPath, include: '**/*.json').each {
                File file = it
                jsonMinified++
                def oldLength = file.length()
                file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
                jsonBytesSaved += oldLength - file.length()
            }
            println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
        }
    }

    tasks.register('optimisePng', com.matyrobbrt.gradle.pngoptimiser.task.OptimisePNGTask) {
        file(project.fileTree(dir: file('common/src/main/resources/assets/doom/textures'), includes: ['**/*.png'])) // Optimise all png files in the `assets` directory
        option 'o', 6 // Use optimisation of type 6
        backupOldFiles() // Backup the png files before optimising them
    }

    // Disables Gradle's custom module metadata from being published to maven. The
    // metadata includes mapped dependencies which are not reasonably consumable by
    // other mod developers.
    tasks.withType(GenerateModuleMetadata).configureEach {

        enabled = false
    }

    // Tells gradle to show 1000 errors instead of the default count of 100.
    // See: https://stackoverflow.com/a/31905248
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "1000"
        }
    }
}