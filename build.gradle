buildscript {
    repositories {
    	maven { url  = 'https://plugins.gradle.org/m2/' }
    }
}

plugins {
    id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id "com.modrinth.minotaur" version "2.+"
}

import groovy.json.JsonSlurper
import groovy.json.JsonOutput
import com.modrinth.minotaur.dependencies.ModDependency

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven { url "https://cfa2.cursemaven.com" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven { url 'https://maven.blamejared.com' }
    maven { url 'https://plugins.gradle.org/m2/' }
    maven { url "https://api.modrinth.com/maven" }
	maven { url = "https://maven.terraformersmc.com/" }
	maven { url = "https://ladysnake.jfrog.io/artifactory/mods" }
	maven { url = "https://maven.kosmx.dev/" }
	maven { url 'https://jitpack.io' }
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "software.bernie.geckolib:geckolib-fabric-1.18:3.0.45"
	modImplementation("curse.maven:modmenu-308702:3472549") {
    	exclude(group: "net.fabricmc.fabric-api")
    }
    modImplementation "dev.emi:trinkets:3.3.0"
	modCompileOnly "curse.maven:RoughlyEnoughItems-310111:3768212"
	modApi("me.shedaniel.cloth:cloth-config-fabric:6.2.57") {
    	exclude(group: "net.fabricmc.fabric-api")
    }
	modCompileOnly "curse.maven:arch-419699:3784130"
	modCompileOnly "curse.maven:stfu-401978:3677546"
    modCompileOnly ("me.shedaniel.cloth:basic-math:0.6.1")
	modCompileOnly "curse.maven:BetterC-639842:3921814"
	modCompileOnly "curse.maven:CConfig-381257:3773665"
    modApi "com.github.ZsoltMolnarrr:TinyConfig:1.0.1"
    modCompileOnly "dev.kosmx.player-anim:player-animation-lib-fabric:0.0.5"
}

processResources {
    inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

processResources {
	doLast {
        def jsonMinifyStart = System.currentTimeMillis()
        def jsonMinified = 0
        def jsonBytesSaved = 0
        fileTree(dir: outputs.files.asPath, include: '**/*.json').each { 
            File file = it
            jsonMinified++
            def oldLength = file.length()
            file.text = JsonOutput.toJson(new JsonSlurper().parse(file))
            jsonBytesSaved += oldLength - file.length()
        }
        println('Minified ' + jsonMinified + ' json files. Saved ' + jsonBytesSaved + ' bytes. Took ' + (System.currentTimeMillis() - jsonMinifyStart) + 'ms.')
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

loom {
	accessWidenerPath = file("src/main/resources/doom.accesswidener")
}

if (file('key.properties').exists()) {
    curseforge {

    	def curseProp = new Properties()
    	File secretPropsFile = file("key.properties")
    	curseProp.load(secretPropsFile.newInputStream())

    	project {
        	apiKey = curseProp.getProperty('curseKey')
        	id = "${curse_id}"
        	releaseType = "${curse_release}"
        	changelogType = 'text'
        	changelog = file('CHANGELOG.txt')
			addGameVersion("1.18.2")
			addGameVersion("Fabric")
			addGameVersion("Java 17")

        	relations {
            	requiredDependency 'fabric-api'  
            	requiredDependency 'geckolib'  
            	requiredDependency 'trinkets'  
            	optionalDependency 'roughly-enough-items' 
            	optionalDependency 'better-combat-by-daedelus' 
        	}
        	mainArtifact(remapJar)

        	afterEvaluate {
            	uploadTask.dependsOn("remapJar")
        	}
    	}
    	options {
    		//debug = true
        	forgeGradleIntegration = false
    	}
    }
}

if (file('key.properties').exists()) {
	modrinth {
		def modrinthProp = new Properties()
		File secretPropsFile = file("key.properties")
		modrinthProp.load(secretPropsFile.newInputStream())
		
		token = modrinthProp.getProperty('modrinthKey')
		projectId = 'Mfo4FiDF' 
		versionNumber = project.mod_version 
		versionName = 'Fabric 1.18.2'
		uploadFile = remapJar 
		changelog = rootProject.file("changelog.txt").text
		gameVersions = ['1.18.2']
		loaders = ['fabric']
		dependencies { 
        	required.project "geckolib"
        	required.project "trinkets"
        	required.project "fabric-api"
        	optional.project "better-combat"
            optional.project "modmenu"
            optional.project "roughly-enough-items"
   		}
	}
}